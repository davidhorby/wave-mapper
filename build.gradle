import org.gradle.api.tasks.testing.logging.TestLogEvent

plugins {
    id "idea"
    id 'java'
    id 'java-test-fixtures'
    alias libs.plugins.kotlinjvm
}

allprojects {
    project.group = "com.dhorby"
}


subprojects {

    apply plugin: 'java'
    apply plugin: 'java-test-fixtures'
    apply plugin: 'kotlin'

    kotlin {
        jvmToolchain(21)
    }

    java {
        consistentResolution {
            useCompileClasspathVersions()
        }
    }

    test {
        useJUnitPlatform()
        testLogging {
            events TestLogEvent.STANDARD_ERROR, TestLogEvent.FAILED//, TestLogEvent.SKIPPED, TestLogEvent.PASSED
            exceptionFormat = "full"

            afterSuite { desc, result ->
                if (!desc.parent) { // will match the outermost suite
                    println ""
                    println "### ${result.resultType} ### (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
                }
            }
        }
    }
}

