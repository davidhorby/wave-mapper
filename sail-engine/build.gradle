plugins {
    id 'com.github.johnrengelman.shadow' version "$johnRengelmanVersion"
    id 'org.jetbrains.kotlin.plugin.serialization' version "$kotlinVersion"
}

configurations {
    invoker
}


shadowJar {
    archiveBaseName.set(project.name)
    archiveClassifier.set(null)
    archiveVersion.set(null)
    mergeServiceFiles()
}


jar {
    manifest {
        attributes 'Main-Class': 'com.dhorby.gcloud.GameEngine'
    }
}


dependencies {

    implementation project(":domain")
    implementation project(":infrastructure")

    implementation kotlinDeps
    implementation libs.bundles.googleCloud
    implementation libs.bundles.jackson
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation 'com.google.cloud.functions:functions-framework-api:1.0.4'
    implementation "org.slf4j:slf4j-simple:$slf4jVersion"

    invoker 'com.google.cloud.functions.invoker:java-function-invoker:1.1.1'

    testImplementation project( path: ":domain", configuration: "testArtifact")

    testImplementation libs.bundles.test
    testRuntimeOnly junit5RuntimeOnly

}

tasks.register("runFunction", JavaExec) {
    main = 'com.google.cloud.functions.invoker.runner.Invoker'
    classpath(configurations.invoker)
    inputs.files(configurations.runtimeClasspath, sourceSets.main.output)
    args(
            '--target', project.findProperty('run.functionTarget'),
            '--port', project.findProperty('run.port') ?: 8080
    )
    doFirst {
        args('--classpath', files(configurations.runtimeClasspath, sourceSets.main.output).asPath)
    }
}
